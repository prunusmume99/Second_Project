#include <Arduino.h>
#include <ArduinoJson.h>
// git clone https://github.com/bblanchon/ArduinoJson
#include <ESP8266WiFi.h> // ESP8266 WiFi ê¸°ëŠ¥ì„ ìœ„í•œ ë¼ì´ë¸ŒëŸ¬ë¦¬
#include <ESPAsyncTCP.h> // ESP8266 ì „ìš© ë¹„ë™ê¸° TCP í†µì‹  ë¼ì´ë¸ŒëŸ¬ë¦¬
// git clone https://github.com/me-no-dev/ESPAsyncTCP.git
#include <MFRC522.h> // MFRC522 RFID ëª¨ë“ˆìš© ë¼ì´ë¸ŒëŸ¬ë¦¬
#include <time.h>    // ì‹œê°„ ê´€ë ¨ í•¨ìˆ˜ ì‚¬ìš©ì„ ìœ„í•œ ë¼ì´ë¸ŒëŸ¬ë¦¬

#define FSR_PIN A0  // NodeMCUì˜ ì•„ë‚ ë¡œê·¸ í•€
#define TOUCH_PIN 4 // D2 (GPIO4)
//  === RFID PIN ì„¤ì • ===
#define RST_PIN 5 // D1 (GPIO5)
#define SS_PIN 2  // D4 (GPIO2)
// #define SCK_PIN 14      // D5 (GPIO14)
// #define MISO_PIN 12     // D6 (GPIO12)
// #define MOSI_PIN 13     // D7 (GPIO13)

MFRC522 rfid(SS_PIN, RST_PIN);

// Wi-Fi ì„¤ì •
const char *ssid = "turtle";                // Wi-Fi SSID
const char *password = "turtlebot3";        // Wi-Fi ë¹„ë°€ë²ˆí˜¸
const char *DESK_ID = "DESK01";             // ê³ ìœ í•œ í´ë¼ì´ì–¸íŠ¸ ID (ì˜ˆ: DESK01, DESK02)
const char *ACTUATOR_IP = "192.168.0.87";   // í˜ì–´ë§ë˜ëŠ” ì•¡ì¸„ì—ì´í„°ì˜ IP
String UID = "";

// TCP í´ë¼ì´ì–¸íŠ¸ ì„¤ì •
AsyncClient client;
const char *remoteHost = "192.168.0.60"; // ë¼ì¦ˆë² ë¦¬ íŒŒì´ ì„œë²„ IP
const int remotePort = 5010;             // ë¼ì¦ˆë² ë¦¬ íŒŒì´ ì„œë²„ í¬íŠ¸
unsigned long lastClientSend = 0;

// === í•¨ìˆ˜ ì„ ì–¸ ===
void handleClientConnect(void *arg, AsyncClient *c);
void handleClientData(void *arg, AsyncClient *c, void *data, size_t len);
void handleClientDisconnect(void *arg, AsyncClient *c);
String getCurrentTime();
void sendToTcpServer(String event, String value = String(0));

// === Sensor Flag ì´ˆê¸°í™” ===
bool auth_flag = false, ping_flag = false;
bool touch_flag = false, fsr_flag = false;
bool record_flag = false, action_flag = false;

// === Touch Sensor Action íŒë³„ìš© ë³€ìˆ˜ ===
enum TouchState
{
    IDLE,
    TOUCHING,
    WAITING_FOR_SECOND_TOUCH,
    COOLDOWN
};
TouchState touchState = IDLE;
unsigned long touchStartTime = 0;
unsigned long lastTouchTime = 0;
unsigned long cooldownStartTime = 0;
bool firstTouchDetected = false;
const unsigned long doubleTouchGap = 250;
const unsigned long longTouchThreshold = 4000;
const unsigned long cooldownDuration = 200;
const int LCD_MODE_COUNT = 3;
int lcd_mode = 0;   // 0 : í˜„ì¬ ì¸¡ì • ì¤‘ì¸ ì‹œê°„, 1 : í•˜ë£¨ë™ì•ˆ ì¸¡ì •ëœ ì´ ì‹œê°„, 2 : ì›”ê°„ í‰ê·  íŒ¨í„´

// === FSR Sensor í‰ê· ê°’ ê³„ì‚° ë³€ìˆ˜ ===
const int FSR_COUNT = 60;
int fsrValues[FSR_COUNT] = {0}; // ìµœê·¼ 60ê°œ ì €ì¥
int fsrIndex = 0;
bool filled = false;

void setup()
{
    pinMode(TOUCH_PIN, INPUT);

    // === SPI & RFID ì´ˆê¸°í™” ===
    SPI.begin();     // SPI í†µì‹  ì„ ë¡œ ì¤€ë¹„ (SPI.h)
    rfid.PCD_Init(); // MFR522 ë ˆì§€ìŠ¤í„°, ì„¤ì • ë“± ë‚´ë¶€ ì´ˆê¸°í™” (MFRC522.h)

    Serial.begin(115200);

    // Wi-Fi ì—°ê²°
    WiFi.begin(ssid, password);
    Serial.print("\nTry WiFi connect");
    while (WiFi.status() != WL_CONNECTED)
    {
        delay(500);
        Serial.print(".");
    }
    Serial.print("\nConnected to WiFi");
    Serial.print(" / IP: ");
    Serial.println(WiFi.localIP());

    // NTP ì„¤ì • (KST ê¸°ì¤€)
    configTime(9 * 3600, 0, "time.nist.gov");
    Serial.print("Try Time Synchronize");
    // ì‹œê°„ ë™ê¸°í™” ëŒ€ê¸°
    while (time(nullptr) < 100000)
    {
        delay(500);
        Serial.print(".");
    }
    Serial.println("\nTime Sync Complete");

    // TCP í´ë¼ì´ì–¸íŠ¸ ì„¤ì •
    client.onConnect(&handleClientConnect, nullptr);
    client.onData(&handleClientData, nullptr);
    client.onDisconnect(&handleClientDisconnect, nullptr);
    client.connect(remoteHost, remotePort);
}

void loop()
{
    static int touchAction = 0;
    static int fsrSum = 0;
    static int fsrAverage = 0;

    if (touch_flag)
    {
        bool isTouched = digitalRead(TOUCH_PIN) == HIGH;
        unsigned long now = millis();

        switch (touchState)
        {
            case IDLE:
                if (touch_flag && isTouched)
                {
                    touchStartTime = now;
                    touchState = TOUCHING;
                }
                break;

            case TOUCHING:
                if (!isTouched)
                {
                    unsigned long duration = now - touchStartTime;
                    if (duration >= longTouchThreshold)
                    {
                        touchAction = 3;  // Long touch
                        touchState = COOLDOWN; // ğŸ’¡ ì¿¨ë‹¤ìš´ ì§„ì…
                        cooldownStartTime = now;
                    }
                    else
                    {
                        lastTouchTime = now;
                        firstTouchDetected = true;
                        touchState = WAITING_FOR_SECOND_TOUCH;
                    }
                }
                break;

            case WAITING_FOR_SECOND_TOUCH:
                if (isTouched && (now - lastTouchTime <= doubleTouchGap))
                {
                    touchAction = 2; // Double touch
                    touchState = COOLDOWN;
                    cooldownStartTime = now;
                    firstTouchDetected = false;
                }
                else if (!isTouched && (now - lastTouchTime > doubleTouchGap))
                {
                    touchAction = 1; // Single short touch
                    touchState = COOLDOWN;
                    cooldownStartTime = now;
                    firstTouchDetected = false;
                }
                break;
            case COOLDOWN:
                if (now - cooldownStartTime > cooldownDuration)
                {
                    touchState = IDLE;
                }
                break;
        }
    }

    if (client.connected() && (millis() - lastClientSend >= 1000))
    {
        ping_flag = true;

        if (!auth_flag)
        {
            if (rfid.PICC_IsNewCardPresent() && rfid.PICC_ReadCardSerial())
            {
                // íƒœê·¸í•œ UIDë¥¼ ë¬¸ìì—´ë¡œ ë³€í™˜
                String tagUID = "";
                for (byte i = 0; i < rfid.uid.size; i++)
                {
                    tagUID += String(rfid.uid.uidByte[i]);
                    if (i < rfid.uid.size - 1)
                        tagUID += " ";
                }

                // ì¹´ë“œ í†µì‹  ì¢…ë£Œ
                rfid.PICC_HaltA();
                rfid.PCD_StopCrypto1();

                sendToTcpServer("rfid", tagUID);
                ping_flag = false;
                
                delay(500); // ì¤‘ë³µ ì¸ì‹ ë°©ì§€
            }
        }

        if (touch_flag && touchAction > 0)
        {
            if (touchAction == 1)       // LCD ì¶œë ¥ ëª¨ë“œ ë³€ê²½
            {
                if (record_flag)
                {
                    lcd_mode = (lcd_mode + 1) % LCD_MODE_COUNT;
                    int touchValue = 10 + lcd_mode;
                    sendToTcpServer("touch", String(touchValue));
                    ping_flag = false;
                }
            }
            else if (touchAction == 2)  // ê³µë¶€ ë£¨í‹´ ê¸°ë¡ ì‹œì‘
            {
                if (!fsr_flag)
                {
                    record_flag = true;
                    fsr_flag = true;
                    memset(fsrValues, 0, sizeof(fsrValues));
                    fsrIndex = 0;
                    filled = false;
                    fsrSum = 0;
                    fsrAverage = 0;

                    sendToTcpServer("touch", String(touchAction));
                    ping_flag = false;
                }
            }
            else if (touchAction == 3)  // ì „ì²´ ì¢…ë£Œ
            {
                auth_flag = false;
                touch_flag = false;
                fsr_flag = false;
                record_flag = false;
                action_flag = false;
                lcd_mode = 0;
                
                sendToTcpServer("touch", String(touchAction));
                ping_flag = false;
                
                UID = "";
                Serial.println("Goodbye My User");
            }

            touchAction = 0;
        }

        if (fsr_flag && record_flag)
        {
            int fsrValue = analogRead(FSR_PIN); // 0 ~ 1023

            fsrSum -= fsrValues[fsrIndex];
            fsrValues[fsrIndex] = fsrValue;
            fsrSum += fsrValues[fsrIndex];

            fsrIndex = (fsrIndex + 1) % FSR_COUNT;
            if (fsrIndex == 0) filled = true;
        
            // í‰ê·  ê³„ì‚°
            int count = filled ? FSR_COUNT : fsrIndex;        
            fsrAverage = fsrSum / count;
            Serial.print("FSR average (");
            Serial.print(fsrIndex);
            Serial.print("): ");
            Serial.println(fsrAverage);

            if (action_flag)
            {
                if (fsrAverage < 30)    // íœ´ì‹ ì‹œê°„ ì¸¡ì •ìœ¼ë¡œ ì „í™˜
                {
                    action_flag = false;
                    sendToTcpServer("action", "0");
                    ping_flag = false;
                }
            }
            else
            {
                if (fsrAverage > 300)   // ê³µë¶€ ì‹œê°„ ì¸¡ì •ìœ¼ë¡œ ì „í™˜
                {
                    action_flag = true;
                    sendToTcpServer("action", "1");
                    ping_flag = false;
                }
            }
        }

        if (ping_flag)
        {
            sendToTcpServer("ping");
        }

        lastClientSend = millis();
    }
}

// í´ë¼ì´ì–¸íŠ¸ ì—°ê²° ì½œë°±
void handleClientConnect(void *arg, AsyncClient *c)
{
    Serial.println("Connected to server");
}

// í´ë¼ì´ì–¸íŠ¸ ë°ì´í„° ìˆ˜ì‹  ì½œë°±
void handleClientData(void *arg, AsyncClient *c, void *data, size_t len)
{
    Serial.printf("Data from server: %.*s\n", len, (char *)data);

    String jsonStr = String((char *)data).substring(0, len);
    jsonStr.trim();  // ê°œí–‰ ë¬¸ì ì œê±°

    StaticJsonDocument<512> resp;  // í•„ìš”í•œ í¬ê¸° ì¡°ì • ê°€ëŠ¥
    DeserializationError error = deserializeJson(resp, jsonStr);

    if (error)
    {
        Serial.print("JSON íŒŒì‹± ì‹¤íŒ¨: ");
        Serial.println(error.c_str());
        return;
    }

    // í•„ë“œ ì¶”ì¶œ
    String event = resp["event"];
    String actuIP = resp["actuIP"];
    String did = resp["did"];
    String uid = resp["uid"];
    int value = resp["value"];
    String timestamp = resp["timestamp"];

    // ì‚¬ìš© ì˜ˆì‹œ
    Serial.print("event: " + event);
    Serial.print("\tactuIP: " + actuIP);
    Serial.print("\tdid: " + did);
    Serial.print("\tuid: " + uid);
    Serial.print("\tvalue: " + String(value));
    Serial.println("\ttimestamp: " + timestamp);

    if (event == "rfid")
    {
        if (value)
        {
            auth_flag = true;
            touch_flag = true;
            UID = uid;
            Serial.println("Welcome My User");
        }
        else
        {
            Serial.println("Please Don't Tag");
        }
    }
}

// í´ë¼ì´ì–¸íŠ¸ ì—°ê²° í•´ì œ ì½œë°±
void handleClientDisconnect(void *arg, AsyncClient *c)
{
    Serial.println("Disconnected from server");
    // ì¬ì—°ê²° ì‹œë„
    client.connect(remoteHost, remotePort);
}

String getCurrentTime()
{
    // í˜„ì¬ ì‹œê°„ êµ¬ì¡°ì²´ë¡œ ê°€ì ¸ì˜¤ê¸°
    time_t now = time(nullptr);
    struct tm *timeinfo = localtime(&now);
    // ISO8601 ë¬¸ìì—´ë¡œ í¬ë§·íŒ…
    char timestamp[25];
    strftime(timestamp, sizeof(timestamp), "%Y-%m-%d %H:%M:%S", timeinfo);

    return String(timestamp);
}

void sendToTcpServer(String event, String value)
{
    String message = "{";
    message += "\"event\":\"" + event + "\",";
    message += "\"actuIP\":\"" + String(ACTUATOR_IP) + "\",";
    message += "\"did\":\"" + String(DESK_ID) + "\",";
    message += "\"uid\":\"" + (event == "rfid" ? value : UID) + "\",";
    message += "\"value\":" + (event == "rfid" ? "0" : value) + ",";
    message += "\"timestamp\":\"" + getCurrentTime() + "\"";
    message += "}";
    client.write((message + "\n").c_str());
    Serial.println("Sent to server: " + message);
}